import com.apollographql.apollo.gradle.internal.ApolloDownloadSchemaTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id "com.google.devtools.ksp"
    id "com.apollographql.apollo3" version "$apollo_version"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.hanmajid.android.pokemonapp"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "GRAPHQL_URL", "\"https://beta.pokeapi.co/graphql/v1beta\""
        }
        release {
            buildConfigField "String", "GRAPHQL_URL", "\"https://beta.pokeapi.co/graphql/v1beta\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        dataBinding true
    }
    applicationVariants.all { variant ->
        variant.sourceSets.java.each {
            it.srcDirs += "build/generated/ksp/${variant.name}/kotlin"
        }
    }
}

dependencies {

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:2.5.0-alpha01"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "com.apollographql.apollo3:apollo-runtime:$apollo_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-annotations:$koin_ksp_version"

    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    ksp "io.insert-koin:koin-ksp-compiler:$koin_ksp_version"

    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    testImplementation "junit:junit:$junit_version"

    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    androidTestImplementation "androidx.test.ext:junit:$test_ext_junit_version"
}

apollo {
    packageName.set("com.hanmajid.android.pokemonapp")
}

tasks.register("_pokemonDownloadSchema", ApolloDownloadSchemaTask) {
    endpoint = "https://beta.pokeapi.co/graphql/v1beta"
    schemaRelativeToProject = "src/main/graphql/com/hanmajid/android/pokemonapp/schema.json"
}